plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.1.7'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.3"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework:spring-web'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL JPA Jakarta
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'

    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // 안전을 위해 추가

}

// =================================================================
// QueryDSL Q클래스 생성을 위한 설정
// =================================================================
def generated = 'build/generated/querydsl'

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// Gradle 7+ 기본 generated 위치를 소스 루트로 추가
sourceSets {
    main.java.srcDirs += [generated]
}

tasks.named("clean") {
    doLast {
        delete(file(generated))
    }
}

